import java.text.SimpleDateFormat

plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

allprojects {
    group 'ikuyo'
    version 'rolling'
    project.ext {
        mode = System.getProperty('mode') ?: 'dev'
        buildTime = new SimpleDateFormat("yyyy/M/dd HH:mm:ss").format(new Date())
    }
}

subprojects {
    apply {
        plugin 'java'
        plugin 'com.github.johnrengelman.shadow'
    }

    sourceCompatibility = JavaVersion.VERSION_19
    targetCompatibility = JavaVersion.VERSION_19

    repositories {
        mavenCentral()
        maven {
            url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    tasks {
        withType(JavaCompile).tap {
            configureEach {
                options.compilerArgs += '--enable-preview'
            }
        }
        withType(JavaExec).tap {
            configureEach {
                jvmArgs += '--enable-preview'
                standardInput = System.in
            }
        }
    }

    dependencies {
        implementation 'io.vertx:vertx-core:4.3.7'
        implementation 'io.vertx:vertx-pg-client:4.3.7'
        implementation 'io.vertx:vertx-hazelcast:4.3.7'
        implementation 'io.vertx:vertx-async-await-incubator:1.0.0-SNAPSHOT'
        implementation 'io.vertx:vertx-execute-blocking-incubator:1.0.0-SNAPSHOT'
        implementation 'com.ongres.scram:client:2.1'
        implementation 'com.google.inject:guice:5.1.0'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.1'
        implementation 'org.jetbrains:annotations:23.0.0'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    }
}

tasks.register('jarAll') {
    dependsOn ':manager:shadowJar', ':server:shadowJar', ':bundle:shadowJar'
}

test {
    useJUnitPlatform()
}
